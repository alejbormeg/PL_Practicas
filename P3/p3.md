---
title: "Procesadores de lenguajes. Práctica 1: Diseño del lenguaje"
description: "Procesadores de Lenguajes"
date: "Curso 2021/2022"
author:
 - "Alejandro Borrego Megías"
 - "Blanca Cano Camarero"
 - "José Luis Ruiz Benito"
output: pdf_document
---

El lenguaje que describimos a continuación **(BBAAC)** está basado en la sintaxis de **C**, con palabras reservadas en **inglés**, a cuyos tipos de datos primitivos añadimos la estructura **lista**, los subprogramas son **funciones** y además de las estructuras de control básicas incluimos la estructura **repeat-until**


## Descripción de la sintaxis en BNF

```
<Programa>  ::= <Cabecera_programa> <bloque>

<bloque>    ::= <Inicio_de_bloque>
                <Declar_de_variables_locales>
                <Declar_de_subprogs>
                <Sentencias>
                <Fin_de_bloque>

<Declar_de_subprogs> ::= <Declar_de_subprogs> <Declar_subprog> |

<Declar_subprog> ::= <Cabecera_subprograma> <bloque>

<Declar_de_variables_locales> ::=   var
                                    <Inicio_de_bloque>
                                    <Variables_locales>
                                    <Fin_de_bloque>
                                    |

<Cabecera_programa> ::= main ()

<Inicio_de_bloque> ::= {
<Fin_de_bloque> ::= }

<Variables_locales> ::= <Variables_locales> <Cuerpo_declar_variables>
                        | <Cuerpo_declar_variables>


<Cuerpo_declar_variables> ::= <tipo> <variables> ;

<tipo> ::= <primitivo> | list of <primitivo>
<primitivo> ::= bool | char | float | int

<variables> ::= <identificador>, <variables> | <identificador>
<identificador> ::= <letra> | <letra> <letra_o_digito>
<letra_o_digito> ::= <letra_o_digito> <letra>
                    | <letra_o_digito> <digito>
                    | <letra>
                    | <digito>

<letra> ::= _ | a | ... | z |
            A | ... | Z
<digito> ::= 0 | 1 | 2 | 3 | 4 | 5 | 6 | 7 | 8 | 9


<Cabecera_subprog> ::=  <tipo> <identificador> (<parametros>)
<parametros> ::= <parametros>, <tipo> <identificador> | <tipo> <identificador>

<Sentencias> ::= <Sentencias> <Sentencia> | <Sentencia>

<Sentencia> ::=   <bloque>
                | <sentencia_asignacion>
                | <sentencia_if> 
                | <sentencia_while>
                | <sentencia_entrada>
                | <sentencia_salida>
                | <sentencia_return>
                | <sentencia_repeat_until>
                | <sentencia_lista>


<sentencia_asignacion> ::= <identificador> = <expresion> ;

<sentencia_if> ::= if (<expresion>) <Sentencia> <sentencia_else>
<sentencia_else> ::=    else <Sentencia>
                        |

<sentencia_while> ::= while (<expresion>) <Sentencia>

<sentencia_entrada> ::= input <lista_id> ;
<lista_id> ::= <lista_id>, <identificador> | <identificador>

<sentencia_salida> ::= output <lista_expresion_cadena> ;
<lista_expresion_cadena> ::=    <lista_expresion_cadena>, <expresion_cadena>
                                | <expresion_cadena>
<expresion_cadena> ::= <expresion> | <cadena>

<sentencia_return> :: return <expresion> ;

<sentencia_repeat_until> ::= repeat <Sentencia> until (<expresion>) ;

<sentencia_lista> ::= <expresion> >> 
                      | <expresion> <<
                      | $ <expresion>

<expresion> ::= ( <expresion> )
                | <op_unario> <expresion>
                | <expresion> <op_binario> <expresion>
                | <expresion> ++ <expresion> @ <expresion>
                | <identificador>
                | <constante>
                | <funcion>

<op_unario> ::= | //
                | not
                | +
                | -
                | ++
                | --
                | #
                | ?

<op_binario> ::=  +
                | -
                | *
                | /
                | %
                | ^
                | ==
                | !=
                | >
                | >=
                | <
                | <=
                | and
                | or
                | xor
                | @
                | --
                | **

<constante> ::=   <entero>
                | <flotante>
                | <booleano>
                | <caracter>
                | <lista>

<entero> ::= <digito> <entero>
            | <digito>

<flotante> ::= <entero> . <entero>
            | . <entero>

<caracter> ::= '<Cualquier caracter ASCII>'

<booleano> ::=    true
                | false

<lista> ::= [ <lista_expresiones> ]
<lista_expresiones> ::= <expresion>, <lista_expresiones> | <expresion> |

<cadena> ::= "<Cadena con cualquier caracter ASCII>"

<funcion> ::= <id> (<lista_expresiones>)

```